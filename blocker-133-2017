#!/usr/bin/perl

use v5.10;
use strict;
use diagnostics;
use warnings;
use Getopt::Std;
use Net::OpenSSH;
use Net::Telnet;
use Net::IP::Match::Regexp qw( create_iprange_regexp match_ip );
use Text::Diff;
use IPC::Open3;

my @asnum = (
	# ( for ASSET in AS-MAILRU AS-VKONTAKTE AS-YANDEX AS-M100; do whois ${ASSET} | awk '$1~/^members:/ && $2~/^AS[0-9]+/ { print $2 }'; done; for MNTBY in YANDEX-MNT KL-MNT MNT-QRATOR-LIR MNT-NETBRIDGE VKONTAKTE-NET-MNT MNT-M100; do whois -h whois.ripe.net -i mnt-by ${MNTBY} -T aut-num | awk '$1~/^aut-num:/ { print $2 }'; done ) | awk '/^AS[0-9]+/' | sed "s/^AS//" | sort -nu | sed "s/^/AS/;s/^/'/;s/$/',/"
	# ( for ASSET in AS-MAILRU AS-VKONTAKTE AS-YANDEX AS-M100; do whois ${ASSET} | awk '$1~/^members:/ && $2~/^AS[0-9]+/ { print $2 }'; done; for MNTBY in YANDEX-MNT KL-MNT MNT-NETBRIDGE VKONTAKTE-NET-MNT MNT-M100; do whois -h whois.ripe.net -i mnt-by ${MNTBY} -T aut-num | awk '$1~/^aut-num:/ { print $2 }'; done ) | awk '/^AS[0-9]+/' | sed "s/^AS//" | sort -nu | sed "s/^/AS/;s/^/'/;s/$/',/"
'AS13238',
'AS20144',
'AS21051',
'AS28709',
'AS41983',
'AS43247',
'AS44903',
'AS47541',
'AS47542',
'AS47764',
'AS49281',
'AS49797',
'AS49988',
'AS51286',
'AS58116',
'AS60476',
'AS60863',
'AS199295',
'AS200107',
'AS200350',
'AS202447',
'AS202611',
'AS205830',
'AS207207'

#    'AS197068', # QRATOR (drweb.ru)
);

my $fhostnames		= 'hostnames';
my $fhostip		= 'hostip';
my $fhostipdiff		= 'hostipdiff';
my $fhostiphostname	= 'hostip.hostname';
my $maxmask		= 33;
my $commitcounter	= 10;

my %opts;
getopts('hu:p:r:om:c:', \%opts);
if (defined $opts{h}) {
    say "$0 -rrouter -uusername -ppassword [-mmask] [-cnum] [-h]

    -r host - адреса маршрутизатора

    -u username - користувач для доступу до маршрутизатора по ssh

    -p password - пароль користувача для доступу до маршрутизатора по ssh

    -o - не считувати файл '$fhostip', брати список виключно з
    конфігурації маршрутизатора

    -m num - максимальна маска (більше 0, але менша чи дорівнює 32).
    Мережі менші за максимальну маску мають ігноруватися.

    -c num - лічильник команд за змінами, що мають виконатися за один
    прохід (за замовченням - 10)

    -h - ця довідка по опціям (вивести та припинити виконання)
";
    exit 253;
}
unless (defined $opts{r} ) {
    say "-rrouter";
    exit 254;
}
unless (defined $opts{u} ) {
    say "-uusername";
    exit 254;
}
unless (defined $opts{p} ) {
    say "-ppassword";
    exit 255;
}
if (defined $opts{m}) {
    $maxmask = int($opts{m}) if int($opts{m})>0 && $opts{m}<33;
}
if (defined $opts{c}) {
    $commitcounter = int($opts{c}) if int($opts{c})>0;
}

my (%hostip, %hostip_jun, $fh, $fho);

if (defined $opts{o} || (-w $fhostip && -s $fhostip==0)) {
    say "Отримуємо список reject-мереж та ip-адрес з бордера…";
    foreach my $netw (getJunOSRejectStaticRoute($opts{r}, $opts{u}, $opts{p})) {
        my ($net, $mask);
        if ($netw =~ m{/}) {
            ($net, $mask) = $netw =~ m{(\d+\.\d+\.\d+\.\d+)/(\d+)?};
        } else {
            ($net, $mask) = ($netw, 32 );
        }
        $hostip_jun{$net} = 32 unless defined $hostip_jun{$net};
        $hostip_jun{$net} = $mask if $mask<$hostip_jun{$net};
    }
} else {
    say "Зчитуємо з файлу '$fhostip' список reject-мереж та ip-адрес…";
    open $fho,"<:utf8", $fhostip or die "Can't open file $fhostip :$!\n";
    while (<$fho>) {
        chomp;
        next if /^#/;
        next if /^$/;

        my ($net, $mask);
        if (m{/}) {
            ($net, $mask) = m{(\d+\.\d+\.\d+\.\d+)/(\d+)?};
        } else {
            ($net, $mask) = ($_, 32 );
        }
        $hostip_jun{$net} = 32 unless defined $hostip_jun{$net};
        $hostip_jun{$net} = $mask if $mask<$hostip_jun{$net};
    }
    close $fho;
}

say "Формуємо список мереж для заданих AS-num…";
foreach my $asnet (@asnum) {
    my @networks = getRTRejectStaticRoute($asnet);
    foreach my $netw ( @networks ) {
        if ($netw !~ m{/32$}) {
            my ($net, $mask) = $netw =~ m{(\d+\.\d+\.\d+\.\d+)/(\d+)};
            $hostip{$net} = 32 unless defined $hostip{$net};
            $hostip{$net} = $mask if $mask<$hostip{$net};
            delete $hostip{$net} if $hostip{$net}>=$maxmask;
        }
    }

}

say "Формуємо regexp для списку мереж…";
my (@ipnet_jun, @ipnet);
foreach my $net ( keys %hostip_jun ) {
    next if $hostip_jun{$net} == 32;
    push @ipnet_jun, sprintf("%s/%s", $net, $hostip_jun{$net});
}
foreach my $net ( keys %hostip ) {
    push @ipnet, sprintf("%s/%s", $net, $hostip{$net});
}
my $ipnet_jun_re = create_iprange_regexp( @ipnet_jun );
my $ipnetre = create_iprange_regexp( @ipnet );

say "Формуємо список ip-адрес для заданих імен хостів і додаємо їх, якщо вони не потрапляють у мережі AS-num…";
if ($maxmask>31) {
    open $fh,"<:utf8", $fhostnames or die "Can't open file $fhostnames :$!\n";
    open $fho,">>:utf8", $fhostiphostname or die "Can't open file $fhostiphostname :$!\n";
    while (<$fh>) {
        next unless /^.+$/;
        next if /^\s*#/;
        chomp;
        my @hosts = getHostIP($_);
        foreach my $ip (@hosts) {
            unless (match_ip($ip, $ipnetre)) {
                unless (match_ip($ip, $ipnet_jun_re)) {
                    $hostip{$ip} = 32;
                    print $fho sprintf("%s %s\n", $ip, $_);
                }
            }
        }
    }


    close $fho;
    close $fh;
}

open $fho,">>:utf8", $fhostiphostname or die "Can't open file $fhostiphostname :$!\n";
    print $fho sprintf("%s %s\n", '104.25.102.28', 'legalizer.info');
    print $fho sprintf("%s %s\n", '104.25.103.28', 'legalizer.info');
close $fho;

system(sprintf("sort -u %s | sort -u -t . -k 1,1n -k 2,2n -k 3,3n -k 4,4n > /tmp/%s", $fhostiphostname, $fhostiphostname."~" ));
system(sprintf("mv /tmp/%s %s", $fhostiphostname."~", $fhostiphostname ));

say "Додаємо мережі та хости у список мереж…";
foreach my $netw ( keys %hostip_jun ) {
    if (defined $hostip{$netw}) {
        if ($hostip{$netw}>$hostip_jun{$netw}) {
            $hostip{$netw} = $hostip_jun{$netw};
            delete $hostip{$netw} if $hostip{$netw}>=$maxmask;
        }
        next;
    }
    unless (match_ip($netw, $ipnetre)) {
        $hostip{$netw} = $hostip_jun{$netw};
        delete $hostip{$netw} if $hostip{$netw}>=$maxmask;
    }
}

say "Формуємо списки для порівняння…";
my (@netlist_old, @netlist_new);
foreach my $netw ( sortipv4( keys %hostip_jun ) ) { push @netlist_old, sprintf("%s/%s", $netw, $hostip_jun{$netw}); }
foreach my $netw ( sortipv4( keys %hostip ) ) { push @netlist_new, sprintf("%s/%s", $netw, $hostip{$netw}); }
my ($netlist_old, $netlist_new) = ( join("\n", @netlist_old), join("\n", @netlist_new) );

say "Проводимо порівняння…";
my $diff = diff \$netlist_old, \$netlist_new;

say "Прибираємо 'зайві' /32…";
foreach my $ip ( sortipv4( keys %hostip_jun ) ) {
    next if $hostip_jun{$ip}<32;
    if (match_ip($ip, $ipnet_jun_re)) {
         $diff .= sprintf("-%s/32\n", $ip);
    }
}

if (length($diff)>0) {
    say "Вносимо зміни до конфігурації…";
    setJunOSConfig($diff, $opts{r}, $opts{u}, $opts{p}, $commitcounter);

    open $fh,">:utf8", $fhostipdiff or die "Can't open file $fhostipdiff :$!\n";
    print $fh $diff;
    close $fh;
}

if (defined $opts{o} || length($diff)>0) {
    say "Записуємо нову версію списку заблокованих мереж та адрес…";
    open $fh,">:utf8", $fhostip or die "Can't open file $fhostip :$!\n";
    print $fh join("\n", getJunOSRejectStaticRoute($opts{r}, $opts{u}, $opts{p}));
    close $fh;
}

say "";

#map {
#    say sprintf("%s/%s", $_, $hostip{$_});
#} sortipv4( keys %hostip );

exit;

sub setJunOSConfig {
    my $diff = shift;
    my $router = shift if @_;
    my $user = shift if @_;
    my $pass = shift if @_;
    my $cc   = shift if @_;

    my ($ret,$c) = ("",0);
e:  foreach my $line ( split("\n", $diff) ) {
        next unless $line =~ /^[+-]/;
        next e if $c>=$cc;
        $line =~ s/;$//;
        if ($line =~ /^-/) {
            $line =~ s/^-//;
            $ret .= sprintf("delete policy-options prefix-list 133-2017 %s\n", $line);
        } else {
            $line =~ s/^\+//;
            $ret .= sprintf("set policy-options prefix-list 133-2017 %s\n", $line);
        }
        $c++;
    }
    doCmd( $router, $user, $pass, $ret );
    return $ret;
}

sub getJunOSRejectStaticRoute {
    my $router = shift if @_;
    my $user = shift if @_;
    my $pass = shift if @_;

    my $ssh = Net::OpenSSH->new( $router, user => $user, password => $pass, master_opts => [-o => "StrictHostKeyChecking=no"] );
    $ssh->error && die "Couldn't establish SSH connection: ". $ssh->error;
    my @ret = split("\n", join("", jCmd($ssh, sprintf("show configuration policy-options prefix-list 133-2017 | no-more")) ));
    map { s,;$,,; } @ret;
    undef $ssh;

    return sortipv4(@ret);
}

sub getRTRejectStaticRoute {
    my $as = shift if @_;
    return "" unless $as;
    my @ret;
#    say sprintf("# %s", $as);
    open (RT, 'echo "@rtconfig printPrefixRanges \"%p/%m\n\" filter '.$as.'" | /usr/local/bin/rtconfig -protocol ripe -config junos |') || die "Failed run rtconfig: $!\n";
    while (<RT>) {
        next unless /^[0-9]/;
        chomp;
        push @ret, $_;
#        say sprintf("\t%s", $_);
    }
    close RT;
    return @ret;
}

sub getHostIP {
    my $host = shift if @_;
    my @ret;

    return @ret unless $host;

    open3(*WHOIS_HOST_IN, *WHOIS_HOST_OUT, *WHOIS_HOST_ERR, sprintf("dig %s -t a \@8.8.8.8 +nottl", $host));
    close(WHOIS_HOST_IN);
    my @outwhoishost = <WHOIS_HOST_OUT>;
    my @errwhoishost = <WHOIS_HOST_ERR>;
    close(WHOIS_HOST_OUT);
    close(WHOIS_HOST_ERR);

    foreach my $line ( @outwhoishost ) {
        next unless $line =~ /^$host\.\s+IN\s+A\s+/;
        chomp $line;
        my (undef, undef, undef, $ip) = split(" ", $line);
        push @ret, $ip;
    }

    return @ret;
}

sub jCmd {
    my $ssh = shift if @_;
    my $cmd = shift if @_;
    my @list = $ssh->capture($cmd);
    $ssh->error && die "remote '$cmd' command failed: " . $ssh->error;
    return @list;
}

sub doCmd {
    my $router = shift if @_;
    my $user = shift if @_;
    my $pass = shift if @_;
    my $cmdin = shift if @_;

    sub diessh {
        map { print; } @_ if @_;
        die;
    }

    return unless length($cmdin)>0;

#   open STDERR, '>', "/dev/null";
    my $ssh = Net::OpenSSH->new( $router, user => $user, password => $pass, master_opts => [-o => "StrictHostKeyChecking=no"] );
    $ssh->error && die "Couldn't establish SSH connection: ". $ssh->error;
    my ($pty, $pid) = $ssh->open2pty();
    my $session = Net::Telnet->new( -fhopen => $pty, -prompt => '/.*[>#]\s+$/', -timeout=>60 );
    $session->waitfor(-match => $session->prompt, -errmode => "return") || diessh "wait failed: " . $session->lastline;
    $session->cmd(String=>'configure private');
    my $cmdc = 0;
    foreach my $cmd ( split "\n", $cmdin ) {
        $session->waitfor(-match => $session->prompt, -errmode => "return") || diessh "wait failed: " . $session->lastline;
        $session->cmd(String=>$cmd);
        say "".$cmd;
        $cmdc++;
        sleep 1;
    }
    $session->waitfor(-match => $session->prompt, -errmode => "return") || diessh "wait failed: " . $session->lastline;
    $session->cmd(String=>'commit and-quit', Timeout=>10*$cmdc);
    $session->waitfor(-match => $session->prompt, -errmode => "return") || diessh "wait failed: " . $session->lastline;
    $session->close;
    waitpid($pid, 0);
    undef $session;
    undef $ssh;
#   open STDERR, '>', "/dev/stderr";
}

sub sortipv4 {
    my @ret = @_;
    map { s/^[^\d]+//; s/[^\d]+$//; s,/32$,,; } @ret;
    @ret = map { join(".",unpack("C5")) } sort map { pack("C5",split /[.\/]/) } @ret;
    map { s,\.(\d+)$,/$1,; s,/0$,,; } @ret;
    return @ret;
}
